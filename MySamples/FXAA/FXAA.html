<!doctype html>
<html>
	<head>
		<title>FXAA</title>
		<script src="./gl-matrix.js"></script>
		<script src="./model-data.js"></script>
		<script src="./mouse-drag.js"></script>
		<script src="./main.js"></script>

		<script id="color-shader-vs" type="x-shader/x-vertex">
		uniform mat4 modelMatrix;
		uniform mat4 invTransModelMatrix;
		uniform mat4 viewMatrix;
		uniform mat4 projectionMatrix;

		attribute vec3 pos;
		attribute vec3 normal;

		varying vec3 fsNormal;
		varying vec3 fsPos;

		void main()
		{
			vec4 worldPos = modelMatrix * vec4(pos, 1);
			gl_Position = projectionMatrix * viewMatrix * worldPos;
			fsPos = worldPos.xyz;
			fsNormal = (invTransModelMatrix * vec4(normal, 0)).xyz;
		}
		</script>
		<script id="color-shader-fs" type="x-shader/x-fragment">
		precision mediump float;

		uniform vec3 meshColor;
		uniform vec3 lightDirection;

		varying vec3 fsNormal;
		varying vec3 fsPos;

		void main()
		{
			vec3 diffuse = (clamp(dot(normalize(fsNormal), normalize(lightDirection)), 0.0, 1.0) * 0.8 + 0.2) * meshColor;
			gl_FragColor = vec4(diffuse, 1.0);
		}
		</script>

		<script id="screen-shader-vs" type="x-shader/x-vertex">
		attribute vec3 pos;
		attribute vec2 uv;

		varying vec2 fsUv;

		void main()
		{
			gl_Position = vec4(pos, 1);
			fsUv = uv;
		}
		</script>
		<script id="screen-shader-fs" type="x-shader/x-fragment">
		precision mediump float;

		uniform sampler2D sampler;

		varying vec2 fsUv;

		void main()
		{
			gl_FragColor = texture2D(sampler, fsUv);
		}
		</script>

		<script id="fxaa-shader-fs" type="x-shader/x-fragment">
		precision mediump float;

		const float FXAA_EDGE_THRESHOLD = 1.0 / 8.0;
		const float FXAA_EDGE_THRESHOLD_MIN = 1.0 / 16.0;
		const float FXAA_SUBPIX_TRIM = 1.0 / 4.0;
		const float FXAA_SUBPIX_CAP = 3.0 / 4.0;
		const float FXAA_SUBPIX_TRIM_SCALE = 1.0 / (1.0 - FXAA_SUBPIX_TRIM);
		const int FXAA_SEARCH_STEPS = 16;
		const float FXAA_SEARCH_THRESHOLD = 1.0 / 4.0;

		uniform sampler2D sampler;
		uniform int mode;
		uniform vec2 rcpFrame;

		varying vec2 fsUv;

		float fxaaLuma(vec3 rgb)
		{
			return rgb.g * (0.587 / 0.299) + rgb.r;
		}

		vec3 fxaaTextureOffset(sampler2D tex, vec2 uv, vec2 offset)
		{
			return texture2D(tex, uv + offset * rcpFrame).rgb;
		}

		void main()
		{
			if(mode == 0)
			{
				gl_FragColor = texture2D(sampler, fsUv);
				return;
			}

			// Step 1: Detect edge

			vec3 rgbN = fxaaTextureOffset(sampler, fsUv, vec2(0, 1));
			vec3 rgbW = fxaaTextureOffset(sampler, fsUv, vec2(-1, 0));
			vec3 rgbM = fxaaTextureOffset(sampler, fsUv, vec2(0, 0));
			vec3 rgbE = fxaaTextureOffset(sampler, fsUv, vec2(1, 0));
			vec3 rgbS = fxaaTextureOffset(sampler, fsUv, vec2(0, -1));
			float lumaN = fxaaLuma(rgbN);
			float lumaW = fxaaLuma(rgbW);
			float lumaM = fxaaLuma(rgbM);
			float lumaE = fxaaLuma(rgbE);
			float lumaS = fxaaLuma(rgbS);
			float rangeMin = min(lumaM, min(min(lumaN, lumaW), min(lumaS, lumaE)));
			float rangeMax = max(lumaM, max(max(lumaN, lumaW), max(lumaS, lumaE)));
			float range = rangeMax - rangeMin;
			if(range < max(FXAA_EDGE_THRESHOLD_MIN, rangeMax * FXAA_EDGE_THRESHOLD))
			{
				gl_FragColor = vec4(rgbM, 1);
				return;
			}

			float lumaL = (lumaN + lumaW + lumaE + lumaS) / 4.0;
			float rangeL = abs(lumaL - lumaM);
			if(mode == 1)
			{
				gl_FragColor = vec4(1, (rangeL / range), 0, 1);
				return;
			}
			float blendL = max(0.0, (rangeL / range) - FXAA_SUBPIX_TRIM) * FXAA_SUBPIX_TRIM_SCALE;
			blendL = min(FXAA_SUBPIX_CAP, blendL);
			vec3 rgbL = rgbN +rgbW + rgbM + rgbE + rgbS;
			vec3 rgbNW = fxaaTextureOffset(sampler, fsUv, vec2(-1, 1));
			vec3 rgbNE = fxaaTextureOffset(sampler, fsUv, vec2(1, 1));
			vec3 rgbSW = fxaaTextureOffset(sampler, fsUv, vec2(-1, -1));
			vec3 rgbSE = fxaaTextureOffset(sampler, fsUv, vec2(1, -1));
			rgbL += rgbNW + rgbNE + rgbSW + rgbSE;
			rgbL /= 9.0;


			// Step 2: Classify pixel as horizontal or vertical
			float lumaNW = fxaaLuma(rgbNW);
			float lumaNE = fxaaLuma(rgbNE);
			float lumaSW = fxaaLuma(rgbSW);
			float lumaSE = fxaaLuma(rgbSE);
			float edgeVert =
				abs((0.25 * lumaNW) + (-0.5 * lumaN) + (0.25 * lumaNE)) +
				abs((0.50 * lumaW ) + (-1.0 * lumaM) + (0.50 * lumaE )) +
				abs((0.25 * lumaSW) + (-0.5 * lumaS) + (0.25 * lumaSE));
			float edgeHorz =
				abs((0.25 * lumaNW) + (-0.5 * lumaW) + (0.25 * lumaSW)) +
				abs((0.50 * lumaN ) + (-1.0 * lumaM) + (0.50 * lumaS )) +
				abs((0.25 * lumaNE) + (-0.5 * lumaE) + (0.25 * lumaSE));
			bool horzSpan = edgeHorz >= edgeVert;
			if(mode == 2)
			{
				if(horzSpan)
					gl_FragColor = vec4(1, 0.75, 0, 1);
				else
					gl_FragColor = vec4(0, 0.5, 1, 1);
				return;
			}

			// Step 3: Determine highest contrast pixel pair
			// Turn vertical case into horizontal case
			float lengthSign = horzSpan ? rcpFrame.y : -rcpFrame.x;
			if(!horzSpan)
			{
				lumaN = lumaW;
				lumaS = lumaE;
			}
			float gradientN = abs(lumaN - lumaM);
			float gradientS = abs(lumaM - lumaS);
			lumaN = (lumaN + lumaM) / 2.0;
			lumaS = (lumaS + lumaM) / 2.0;
			bool pairN = gradientN > gradientS;
			if(mode == 3)
			{
				if(pairN)
					gl_FragColor = vec4(0, 1, 0, 1);
				else
					gl_FragColor = vec4(0, 0, 1, 1);
				return;
			}

			// Step 4: Search for end-of-edge in both the negative and positive directions along the edge
			// Turn both cases {pairN, !pairN} into one case
			if(!pairN)
			{
				lumaN = lumaS;
				gradientN = gradientS;
				lengthSign *= -1.0;
			}
			vec2 posN;
			posN.x = fsUv.x + (horzSpan ? 0.0 : lengthSign * 0.5);
			posN.y = fsUv.y + (horzSpan ? lengthSign * 0.5 : 0.0);

			gradientN *= FXAA_SEARCH_THRESHOLD;

			vec2 posP = posN;
			vec2 offNP = horzSpan ? vec2(rcpFrame.x, 0) : vec2(0, rcpFrame.y);
			float lumaEndN = lumaN, lumaEndP = lumaN;
			bool doneN = false, doneP = false;
			posN -= offNP;
			posP += offNP;
			for(int i = 0; i < FXAA_SEARCH_STEPS; ++i)
			{
				if(!doneN)
					lumaEndN = fxaaLuma(texture2D(sampler, posN).rgb);
				if(!doneP)
					lumaEndP = fxaaLuma(texture2D(sampler, posP).rgb);
				doneN = doneN || (abs(lumaEndN - lumaN) >= gradientN);
				doneP = doneP || (abs(lumaEndP - lumaN) >= gradientN);
				if(doneN && doneP)
					break;
				if(!doneN)
					posN -= offNP;
				if(!doneP)
					posP += offNP;
			}

			float dstN = horzSpan ? fsUv.x - posN.x : fsUv.y - posN.y;
			float dstP = horzSpan ? posP.x - fsUv.x : posP.y - fsUv.y;
			bool directionN = dstN < dstP;
			if(mode == 4)
			{
				if(directionN)
					gl_FragColor = vec4(1, 0, 0, 1);
				else
					gl_FragColor = vec4(0, 0, 1, 1);
				return;
			}

			// Step 5: interpolate pixel to reduce aliasing
			lumaEndN = directionN ? lumaEndN : lumaEndP;
			if((lumaM < lumaN) == (lumaEndN < lumaN))
			{
				lengthSign = 0.0;
			}
			float spanLength = dstP + dstN;
			dstN = directionN ? dstN : dstP;
			float subPixelOffset = (0.5 + (dstN * (-1.0 / spanLength))) * lengthSign;

			vec3 rgbF = texture2D(sampler, vec2(fsUv.x + (horzSpan ? 0.0 : subPixelOffset), fsUv.y + (horzSpan ? subPixelOffset : 0.0))).rgb;
			gl_FragColor = vec4(mix(rgbF, rgbL, blendL), 1);
		}
		</script>
	</head>

	<body onload="start()">
		<span id="label-fps">FPS: 0</span>
		<input type="checkbox" id="checkbox-scale" onclick="toggleScale()"/> 4X Scale
		<select id="select-mode" onchange="changeMode()">
			<option>No FXAA</option>
			<option>Edge Detection</option>
			<option>Edge Orientation</option>
			<option>Edge Neighbor</option>
			<option>Edge Neg/Pos</option>
			<option>FXAA</option>
		</select>
		<br/>
	    <canvas id="glcanvas" width="512" height="512">
			Your browser doesn't appear to support the <code>&lt;canvas&gt;</code> element.
    	</canvas>
    </body>
</html>
